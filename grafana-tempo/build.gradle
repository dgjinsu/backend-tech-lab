plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

bootJar.enabled = false;

subprojects {
	group = 'com.example'
	version = '0.0.1-SNAPSHOT'

	apply {
		plugin 'java'
		plugin 'org.springframework.boot'
		plugin 'io.spring.dependency-management'
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
		agent
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		agent "io.opentelemetry.javaagent:opentelemetry-javaagent:1.32.0"

		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		// rabbitmq
		implementation 'org.springframework.boot:spring-boot-starter-amqp'
		testImplementation 'org.springframework.amqp:spring-rabbit-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	tasks.register("copyAgent", Copy) {
		from configurations.agent {
			rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
		}
		into layout.buildDirectory.dir("agent")
	}

	bootJar {
		dependsOn copyAgent

		archiveFileName = "app.jar"
	}
}
