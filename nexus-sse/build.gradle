plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '1.0.1'
description = 'nexus-sse'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// 라이브러리로 배포하기 위한 설정
bootJar {
    enabled = false
}

jar {
    enabled = true
}

// Nexus 배포 설정
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.example'
            artifactId = 'nexus-sse'
            version = '1.0.1'

            // 빌드 시점에 결정된 버전을 배포 메타데이터에도 사용
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            name = 'nexus'
            url = 'http://localhost:8070/repository/maven-releases/'
            allowInsecureProtocol = true  // HTTP 허용

            credentials {
                username = findProperty('nexusUsername') ?: 'admin'
                password = findProperty('nexusPassword') ?: 'admin123'
            }
        }
    }
}
